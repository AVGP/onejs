// Copyright (C) 2014 OneJS
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//       http://www.apache.org/licenses/LICENSE-2.0
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.

import gl
import math
import constraint 
import input
import glyphy

class Scene extends Device{

	mouse = Mouse.new()
	midi = Midi.new()

	m_camera = mat4()

	// set up a constraint solver
	solver = CS.SimplexSolver.new()

	// when we are adding a node we get called
	onAddChild(node){
		// lets process some constraints
		var proto = node
		while(proto && proto.__constraints__){
			if(proto.hasOwnProperty('__constraints__')){
				for(var c from proto.__constraints__){
					var constraint = CS.ASTNodeToConstraint(node, c)
					var storage = node.__constraint_store__ || (node.__constraint_store__ = [])
					storage.push(constraint)
					solver.addConstraint(constraint)
				}
			}
			proto = Object.getPrototypeOf(proto)
		}
		if(!layout_timer) layout_timer = setTimeout(onLayout.bind(this))
	}

	onLayout(){
		layout_timer = undefined
		solver.solve()
		// alright so.. how do we get those pesky values to update?
		redraw()
	}

	// allright so lets solve our layout..
	m_view = mat4.identity()//mat4.RT(0,0,0, 0,0,3)
	draw:(){
		vec3 f = fill
		clearAll(f)
		m_proj = mat4.ortho(0, frame.w / frame.ratio, 0, frame.h / frame.ratio, -1000, 1000)
		//m_proj = mat4.perspective(-90deg, -1, -1000, 1000) * mat4.S(0.01,0.01,0.01)
		// silently assign camera matrix
		__m_camera = mat4 m_view * mat4 m_proj 
		m_childworld = mat4.identity()
		drawChildren(this) 
	}
}

// a layer with matrices
class Layer extends Draw{

	// forward the mouse object
	mouse = Scene.mouse
	midi = Scene.midi
	// define object types
	mesh = vec2Rect(0, 0, 1, 1)

	// type them, otherwise we cant use it in the compiler
	m_world = mat4()
	m_mesh = mat4.identity()
	m_child = mat4.identity()

	vertex: mesh[] * (m_mesh * m_world * device.m_camera)

	p: pixel mesh[]
	hp: 2.*(pixel mesh[] -.5)

	draw:(parent){
		mat4 m_parent = parent.m_childworld
		// set our matrix
		var dx = ax*w, dy = ay*h, dz = az*d
		mat4 m_local = mat4.TSRT(-dx,-dy,-dz ,sx,sy,sz, rx,ry,rz, x+dx,y+dy,z+dz)

		// scale our mesh
		m_mesh = mat4.S(w, h, d)

		// generate our world matrix
		m_world = m_local * m_parent

		// draw ourselves
		this{}
		// now lets make these things work
		if(children){
			m_childworld = m_child * m_world
			drawChildren(this)
		}
	}
	// anchor
	ax = 0.5
	ay = 0.5
	az = 0
	// rotation
	rx = 0
	ry = 0
	rz = 0
	// scale
	sx = 1
	sy = 1
	sz = 1
	// coordinates
	x = 0
	y = 0
	z = 0
	// size
	w = 100
	h = 100
	d = 0
}

class Text extends Layer{
	load(Glyphy)
	color: #white
	vertex: glyphy_pos * (m_mesh * m_world * device.m_camera)
	pixel: glyphy_color
}