import math
import gl
import input

var midi = Midi.new()

define spiral(vec2 p, float r, float t){
	float len = length(p) * r
	float cl = cos(len+t)
	float sl = sin(len+t)
	return vec2(p.x * cl - p.y * sl, 
				p.x * sl + p.y * cl)
}

define warp(vec2 p){
	return vec2(
		p.x + 0.002*sin(10.*p.x+4.*time.sec),
		p.y + 0.002*cos(10.*p.y+4.*time.sec))
}

class Rect extends Draw{
	mesh = vec2Rect(-.5, -.5, 1, 1)
	vertex:mesh[]	
	pixel:#red
}

class Blur extends Draw{
	buf = Texture.rgba_depth_flipped(.5,.5)
	mesh = vec2Rect(-1, -1, 2, 2)
	vertex: mesh[]
	pixel:	buf.sample(pixel mesh[]*.5+.5)

	pass:(){
		buf{
			clearDepth()
			this{
				pixel:buf.sample(warp(pixel mesh[]*0.49+0.5))
			}
			drawChildren(this)
		}
	}
	draw:(){
		this{}
		drawChildren(this)
	}
}

class Test extends Draw{
	viewmtx: mat4.perspective(70deg, 1, -1, 1)
	objmtx: mat4.RT(10deg, 90deg*time.sec, time.sec, 0, 0, 3+ 35*abs on sin(time.sec))

	mesh = vec2Rect(-2, -2, 4, 4)
	vertex: mesh[] * (objmtx * viewmtx)
	p:pixel mesh[]
	
	myColor:(vec2 p, float anim){
		return (
		(mix(#orange, #black) do 
			abs(sin(param1 * 1. * atan(p.x, p.y))))
		+
		mix(#white, #green) do
			sin(sin(param1 + anim) * param1 * p.x * p.y)
		)
	}

	param1 = 1
	bg = Texture.url('test_texture.jpg')
	sp:spiral(p, 2., time.sec)
	pixel: bg.sample(.25p + .5) **
		myColor(sp, time.sec * 1.)

	draw:(){
		this{}
	}
}

Device{
	fill = #000
	Blur{
		Test{}
	}
}